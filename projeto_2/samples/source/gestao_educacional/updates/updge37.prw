#INCLUDE "Protheus.ch"                                                 
#include "rwmake.ch"

/*


ͻ
Programa  UpdGE37   Autor  Denis D. Almeida     Data   11/04/08   
͹
Desc.     Ajuste de consultas relativas ao CEP   					  
          													          
͹
Uso        GE                                                         
ͼ


*/
User Function UpdGE37() //Para maiores detalhes sobre a utilizacao deste fonte leia o boletim

cArqEmp := "SigaMAT.Emp"
__cInterNet := Nil

PRIVATE cMessage
PRIVATE aArqUpd	 := {}
PRIVATE aREOPEN	 := {}
PRIVATE oMainWnd

Set Dele On

lHistorico 	:= MsgYesNo("Deseja efetuar a atualizacao do dicionrio? Esta rotina deve ser utilizada em modo exclusivo! Faca um backup dos dicionrios e da base de dados antes da atualizao para eventuais falhas de atualizao!", "Ateno")

If lHistorico
	Processa({|lEnd| GEProc(@lEnd)},"Processando","Aguarde, preparando os arquivos",.F.)
endif
	
Return

/*

Ŀ
Funo    GEProc     Autor Denis D. Almeida       Data  14/04/08 
Ĵ
Descrio  Funcao de processamento da gravacao dos arquivos           
Ĵ
 Uso       Atualizacao GE                                             
ٱ

*/
Static Function GEProc(lEnd)
Local cTexto    := '' 				//Exibira o log ao final do processo
Local cFile     :="" //Nome do arquivo, caso o usuario deseje salvar o log das operacoes
Local cMask     := "Arquivos Texto (*.TXT) |*.txt|"
Local nRecno    := 0
Local nI        := 0                //Contador para laco
Local nX        := 0	            //Contador para laco
Local aRecnoSM0 := {}				     
Local lOpen     := .F. 				//Retorna se conseguiu acesso exclusivo a base de dados
Local nModulo := 49 				//SIGAGE - GESTAO EDUCACIONAL

/********************************************************************************************
Inicia o processamento.
********************************************************************************************/
ProcRegua(1)
IncProc("Verificando integridade dos dicionrios....")
If ( lOpen := MyOpenSm0Ex() )

	dbSelectArea("SM0")
	dbGotop()
	While !Eof() 
  		If Ascan(aRecnoSM0,{ |x| x[2] == M0_CODIGO}) == 0 //--So adiciona no aRecnoSM0 se a empresa for diferente
			aAdd(aRecnoSM0,{Recno(),M0_CODIGO})
		EndIf			
		dbSkip()
	EndDo	
		
	If lOpen
		For nI := 1 To Len(aRecnoSM0)

			SM0->(dbGoto(aRecnoSM0[nI,1]))
			RpcSetType(2) 
			RpcSetEnv(SM0->M0_CODIGO, SM0->M0_CODFIL)
			lMsFinalAuto := .F.
			cTexto += Replicate("-",128)+CHR(13)+CHR(10)
			cTexto += "Empresa : "+SM0->M0_CODIGO+SM0->M0_NOME+CHR(13)+CHR(10)
			
			//Ŀ
			//Atualiza o dicionario de arquivos.
			//
			IncProc( dtoc( Date() )+" "+Time()+" "+"Inicio - Dicionario de Arquivos")			
			cTexto += "Analisando Dicionario de Arquivos..."+CHR(13)+CHR(10)
			GEAtuSX2()
			IncProc( dtoc( Date() )+" "+Time()+" "+"Fim - Dicionario de Arquivos")
			
			//Ŀ
			//Atualiza o dicionario de dados.
			//
			IncProc("Analisando Dicionario de Dados...")
			cTexto += GEAtuSX3()
             
			//Ŀ
			//Atualiza os indices.
			//
			IncProc( dtoc( Date() )+" "+Time()+" "+"Inicio - Indices")
			cTexto += "Analisando arquivos de ndices. "+CHR(13)+CHR(10)
			GEAtuSIX()
			IncProc( dtoc( Date() )+" "+Time()+" "+"Fim - Indices")

			//Ŀ
			//Atualiza Consultas  
			//
			IncProc( dtoc( Date() )+" "+Time()+" "+"Inicio - Consultas Padroes")
			cTexto += "Analisando consultas padroes..."+CHR(13)+CHR(10) 
			GEAtuSxB( SM0->M0_CODIGO )
			IncProc( dtoc( Date() )+" "+Time()+" "+"Fim - Consultas Padroes")

			__SetX31Mode(.F.)
			For nX := 1 To Len(aArqUpd)
				IncProc("Atualizando estruturas. Aguarde... ["+aArqUpd[nx]+"]")
				If Select(aArqUpd[nx])>0
					dbSelecTArea(aArqUpd[nx])
					dbCloseArea()
				EndIf
				X31UpdTable(aArqUpd[nx])
				If __GetX31Error()
					Alert(__GetX31Trace())
					Aviso("Atencao!","Ocorreu um erro desconhecido durante a atualizacao da tabela : "+ aArqUpd[nx] + ". Verifique a integridade do dicionario e da tabela.",{"Continuar"},2)
					cTexto += "Ocorreu um erro desconhecido durante a atualizacao da estrutura da tabela : "+aArqUpd[nx] +CHR(13)+CHR(10)
				EndIf
			Next nX

			//Utiliza o Select Area para forcar a criacao das tabelas
			dbSelectArea("JC2")
			
			RpcClearEnv()
			If !( lOpen := MyOpenSm0Ex() )
				Exit 
			EndIf 
		Next nI 
		   
		If lOpen
			
			cTexto := "Log da atualizacao "+CHR(13)+CHR(10)+cTexto
			__cFileLog := MemoWrite(Criatrab(,.f.)+".LOG",cTexto)
			DEFINE FONT oFont NAME "Mono AS" SIZE 5,12   //6,15
			DEFINE MSDIALOG oDlg TITLE "Atualizacao concluida." From 3,0 to 340,417 PIXEL
			@ 5,5 GET oMemo  VAR cTexto MEMO SIZE 200,145 OF oDlg PIXEL
			oMemo:bRClicked := {||AllwaysTrue()}
			oMemo:oFont:=oFont
			DEFINE SBUTTON  FROM 153,175 TYPE 1 ACTION oDlg:End() ENABLE OF oDlg PIXEL //Apaga
			DEFINE SBUTTON  FROM 153,145 TYPE 13 ACTION (cFile:=cGetFile(cMask,""),If(cFile="",.t.,MemoWrite(cFile,cTexto))) ENABLE OF oDlg PIXEL //Salva e Apaga //"Salvar Como..."
			ACTIVATE MSDIALOG oDlg CENTER
			
		EndIf 
		
	EndIf
		
EndIf 	

Return(.T.)

/*

Ŀ
Funo    GEAtuSX2   Autor Denis D. Almeida       Data  11/04/08  
Ĵ
Descrio  Funcao de processamento da gravacao do SX2     			   
                       												   
Ĵ
 Uso       Atualizacao GE                                             
ٱ

*/
Static Function GEAtuSX2()
Local aSX2   := {}							//Array que contem as informacoes das tabelas
Local aEstrut:= {}							//Array que contem a estrutura da tabela SX2
Local i      := 0							//Contador para laco
Local j      := 0							//Contador para laco
Local cAlias := ''     						//Nome da tabela
Local cPath									//String para caminho do arquivo 
Local cNome									//String para nome da empresa e filial
Local cModo        
Local cQuery := ""
Local cDel   := ""
Local lMSSQL := "MSSQL"$Upper(TCGetDB())							//Variaveis para tratamento de Banco de dados
Local lMySQL := "MYSQL"$Upper(TCGetDB())							//Variaveis para tratamento de Banco de dados 

aEstrut:= {"X2_CHAVE","X2_PATH","X2_ARQUIVO","X2_NOME","X2_NOMESPA","X2_NOMEENG","X2_DELET","X2_MODO","X2_TTS","X2_ROTINA","X2_UNICO","X2_PYME"}

ProcRegua(Len(aSX2))

dbSelectArea("SX2")
SX2->(DbSetOrder(1))	
MsSeek("JAH") //Seleciona a tabela que eh padrao do sistema para pegar algumas informacoes
cPath := SX2->X2_PATH
cNome := Substr(SX2->X2_ARQUIVO,4,5)
cModo := SX2->X2_MODO

cQuery := " SELECT	 JC2_CEP,JC2_LOGRAD,JC2_BAIRRO,JC2_CIDADE,JC2_ESTADO,MAX(R_E_C_N_O_) REC  "
cQuery += "     FROM "+retsqlname("JC2")+" "
cQuery += "   WHERE  JC2_FILIAL='"+xfilial("JC2")+"' "
cQuery += " GROUP BY JC2_CEP,JC2_LOGRAD,JC2_BAIRRO,JC2_CIDADE,JC2_ESTADO  "
if lMSSQL
	cQuery += " HAVING  COUNT(JC2_CEP+JC2_LOGRAD+JC2_BAIRRO+JC2_CIDADE+JC2_ESTADO) > 1 "             
elseif lMySQL                                                                                        
	cQuery += " HAVING  COUNT(concat(JC2_CEP,JC2_LOGRAD,JC2_BAIRRO,JC2_CIDADE,JC2_ESTADO)) > 1 "             
else                                                                                                 
	cQuery += " HAVING  COUNT(JC2_CEP||JC2_LOGRAD||JC2_BAIRRO||JC2_CIDADE||JC2_ESTADO) > 1 "             
endif 
cQuery := ChangeQuery( cQuery )
if select("JC2DEL") > 0
	JC2DEL->( dbCloseArea() )
endif
dbUseArea( .T., "TopConn", TCGenQry(,,cQuery), "JC2DEL", .F., .F. )

JC2DEL->( dbgotop() )
while JC2DEL->( !eof() )
	cDel := " DELETE "
	cDel += "    FROM "+retsqlname("jc2")+" "
	cDel += "   WHERE JC2_FILIAL='"+xFilial("JC2")+"' "
	cDel += "     AND R_E_C_N_O_ IN ('"+alltrim(str(JC2DEL->rec))+"') "
	TcSqlExec(cDel)
	TcSqlExec("commit")
	JC2DEL->( dbskip() )
end

/******************************************************************************************
* Adiciona as informacoes das tabelas num array, para trabalho posterior
*******************************************************************************************/
aAdd(aSX2,{	"JC2",; 								//Chave
			cPath,;									//Path
			"JC2"+cNome,;							//Nome do Arquivo
			"Cadastro de Ceps",;				//Nome Port
			"Cadastro de Ceps",;				//Nome Port
			"Cadastro de Ceps",;				//Nome Port
			0,;										//Delete
			cModo,;									//Modo - (C)Compartilhado ou (E)Exclusivo
			"",;									//TTS
			"",;									//Rotina
			"JC2_FILIAL+JC2_CEP+JC2_LOGRAD+JC2_BAIRRO+JC2_CIDADE+JC2_ESTADO",;				//Unico
			"S"})									//Pyme
/*******************************************************************************************
Realiza a inclusao das tabelas
*******************************************/
For i:= 1 To Len(aSX2)
	If !Empty(aSX2[i][1])
		If SX2->( !dbSeek(aSX2[i,1]) )
			RecLock("SX2",.T.) //Adiciona registro		
		Else
			RecLock("SX2",.F.) //Altera Registro
		EndIf		
		If !(aSX2[i,1]$cAlias)
			cAlias += aSX2[i,1]+"/"
		EndIf
		For j:=1 To Len(aSX2[i])
			If FieldPos(aEstrut[j]) > 0
				FieldPut(FieldPos(aEstrut[j]),aSX2[i,j])
			EndIf
		Next j
		SX2->X2_PATH := cPath
		SX2->X2_ARQUIVO := aSX2[i,1]+cNome
		MsUnLock()
		IncProc("Atualizando Dicionario de Arquivos...")
	EndIf
Next i

Return


/*

Ŀ
Funo    GEAtuSX3   Autor Denis D. Almeida       Data  11/04/08  
Ĵ
Descrio  Funcao de processamento da gravacao do SX3 - Campos        
Ĵ
 Uso       Atualizacao GE                                             
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador   Data    BOPS   Motivo da Alteracao                    
Ĵ


/*/
Static Function GEAtuSX3()
Local aEstrut        := {}              //Array com a estrutura da tabela SX3
Local i              := 0				//Laco para contador
Local lSX3	         := .F.             //Indica se houve atualizacao
Local cTexto         := ''				//String para msg ao fim do processo
Local cUsadoOpc		 := ''				//String que servira para cadastrar um campo como "USADO" em campos opcional
Local cReservOpc	 := ''				//String que servira para cadastrar um campo como "Reservado" em campos opcional
Local cCampMens      := "" 
Local aCamposAl      := {}
/*******************************************************************************************
Define a estrutura do array
*******************************************/
aEstrut:= { "X3_ARQUIVO","X3_ORDEM"  ,"X3_CAMPO"  ,"X3_TIPO"   ,"X3_TAMANHO","X3_DECIMAL","X3_TITULO" ,"X3_TITSPA" ,"X3_TITENG" ,;
			"X3_DESCRIC","X3_DESCSPA","X3_DESCENG","X3_PICTURE","X3_VALID"  ,"X3_USADO"  ,"X3_RELACAO","X3_F3"     ,"X3_NIVEL"  ,;
			"X3_RESERV" ,"X3_CHECK"  ,"X3_TRIGGER","X3_PROPRI" ,"X3_BROWSE" ,"X3_VISUAL" ,"X3_CONTEXT","X3_OBRIGAT","X3_VLDUSER",;
			"X3_CBOX"   ,"X3_CBOXSPA","X3_CBOXENG","X3_PICTVAR","X3_WHEN"   ,"X3_INIBRW" ,"X3_GRPSXG" ,"X3_FOLDER", "X3_PYME"}

dbSelectArea("SX3")
SX3->(DbSetOrder(2))//X3_CAMPO

/*******************************************************************************************
Seleciona as informacoes de alguns campos para uso posterior
*******************************************/
If SX3->( dbSeek("JAE_DISPAI") ) //Este campo eh Opcional e permite alterar
	cUsadoOpc	:= SX3->X3_USADO
	cReservOpc	:= SX3->X3_RESERV
EndIf
If SX3->( dbSeek("JBF_DGRPDO") ) //Este campo eh Opcional e visualizar
	cUsadoVis	:= SX3->X3_USADO
	cReservVis	:= SX3->X3_RESERV
EndIf
//Campos que devem sofrer alterao
Aadd( aCamposAl, "JA1_CEP   " ) 
Aadd( aCamposAl, "JA1_CEPPRF" )
Aadd( aCamposAl, "JA2_CEP   " )
Aadd( aCamposAl, "JA2_CEPPRF" )
Aadd( aCamposAl, "JA3_CEP   " )
Aadd( aCamposAl, "JA4_CEP   " )
Aadd( aCamposAl, "JCL_CEP   " )
Aadd( aCamposAl, "JCR_CEP   " )
Aadd( aCamposAl, "JCR_CEPPRF" )
Aadd( aCamposAl, "JHP_CEP   " )
Aadd( aCamposAl, "JC1_CEP   " )

SX3->( DbSetOrder(2) )             
for i := 1 to len(aCamposAl)
	If SX3->( DbSeek(aCamposAl[i]) ) .And. SX3->X3_F3 <> "JC2_CT"
		RecLock("SX3", .F.)
		Replace X3_F3 	With "JC2_CT"
		SX3->( MsUnlock() )
	Endif
	cCampMens += alltrim(aCamposAl[i])+CHR(13)+CHR(10)
next i

If lSX3
	cTexto := 'Foi alterada a estrutura dos seguintes campos de Dicionrio : '+cCampMens
EndIf

Return cTexto


/*

Ŀ
Funo    GEAtuSXB  Autora Denis D. Almeida        Data 11/04/08  
Ĵ
Descrio  Funcao para atualizao das consultas padroes do sistema   
           												              
Ĵ
 Uso       Atualizacao GE                                             
ٱ

*/
Static Function GEAtuSXB(cCodFilial)
Local lSXB      := .F.                      //Verifica se houve atualizacao
Local aSXB      := {}						//Array que armazenara os indices
Local aEstrut   := {}				        //Array com a estrutura da tabela SXB
Local i         := 0 						//Contador para laco
Local j         := 0 						//Contador para laco

//Ŀ
//Define estrutura do array
//
aEstrut:= {"XB_ALIAS", "XB_TIPO", "XB_SEQ", "XB_COLUNA", "XB_DESCRI", "XB_DESCSPA", "XB_DESCENG", "XB_CONTEM", "XB_WCONTEM"}

//Ŀ
//Define os novos conteudos dos filtros das consultas
//
aAdd(aSXB,{	"JC2_CT","1","01","RE",;		// ALIAS, TIPO, SEQ, COLUNA
			"Consulta - Cadastro de CEP",;		// DESCRI
			"Consulta - Cadastro de CEP",;		// DESCRI
			"Consulta - Cadastro de CEP",;		// DESCRI
			"JC2", "" })				// CONTEM, WCONTEM  

aAdd(aSXB,{	"JC2_CT","2","01","01",;		// ALIAS, TIPO, SEQ, COLUNA
			"",;						// DESCRI
			"",;						// DESCRI
			"",;						// DESCRI
			"AcSxbCepGr()", "" })		// CONTEM, WCONTEM

aAdd(aSXB,{	"JC2_CT","5","01","",;		// ALIAS, TIPO, SEQ, COLUNA
			"",;						// DESCRI
			"",;						// DESCRI
			"",;						// DESCRI
			"JC2->JC2_CEP", "" })			// CONTEM, WCONTEM	   
			
aAdd(aSXB,{	"JC2_CT","5","02","",;		// ALIAS, TIPO, SEQ, COLUNA
			"",;						// DESCRI
			"",;						// DESCRI
			"",;						// DESCRI
			"JC2->JC2_LOGRAD", "" })			// CONTEM, WCONTEM	   			
//Ŀ
//Processa consultas para alteracao                  
//
ProcRegua(Len(aSXB))

dbSelectArea("SXB")
SXB->(dbSetOrder(1)) //XB_ALIAS+XB_TIPO+XB_SEQ+XB_COLUNA	
For i := 1 To Len(aSXB)
	If !DbSeek(PadR(aSXB[i,1],6)+aSXB[i,2]+aSXB[i,3]+aSXB[i,4])
		RecLock("SXB",.T.)
	Else
		RecLock("SXB",.F.)
	EndIf
	lSxB := .T.
	For j:=1 To Len(aSXB[i])
		If FieldPos(aEstrut[j]) > 0
			FieldPut(FieldPos(aEstrut[j]),aSXB[i,j])
		EndIf
	Next j
	MsUnLock()
	IncProc("Atualizando consulta padrao...")
Next i

Return

/*

Ŀ
Funo    GEAtuSIX   Autor Denis D. Almeida       Data  11/04/08  
Ĵ
Descrio  Funcao de processamento da gravacao do SIX - Indices       
Ĵ
 Uso       Atualizacao GE                                             
ٱ

*/
Static Function GEAtuSIX()
//INDICE ORDEM CHAVE DESCRICAO DESCSPA DESCENG PROPRI F3 NICKNAME SHOWPESQ
Local lSix      := .F.                      //Verifica se houve atualizacao
Local aSix      := {}						//Array que armazenara os indices
Local aEstrut   := {}				        //Array com a estrutura da tabela SiX
Local i         := 0 						//Contador para laco
Local j         := 0 						//Contador para laco
Local cAlias    := ''						//Alias para tabelas
Local lDelInd   := .F.

/*********************************************************************************************
Define estrutura do array
*********************************************************************************************/
aEstrut:= {"INDICE","ORDEM","CHAVE","DESCRICAO","DESCSPA","DESCENG","PROPRI","F3",;
		   "NICKNAME","SHOWPESQ"}

aAdd(aSIX,{	"JC2",;   										   													  //Indice
			"1",;                 							   													  //Ordem
			"JC2_FILIAL+JC2_CEP+JC2_LOGRAD+JC2_BAIRRO+JC2_CIDADE+JC2_ESTADO",; //Chave
			"CEP + Logradouro + Bairro + Cidade + Estado",;		                      //Descicao Port.
			"CEP + Logradouro + Bairro + Cidade + Estado",;			                  //Descicao Port.
			"CEP + Logradouro + Bairro + Cidade + Estado",;		                	  //Descicao Port.
			"S",; 																								  //Proprietario
			"",;  																								  //F3
			"",;  																								  //NickName
			"S"}) 												                                                  //ShowPesq  
/*********************************************************************************************
Grava as informacoes do array na tabela six
*********************************************************************************************/
ProcRegua(Len(aSIX))

dbSelectArea("SIX")
SIX->(DbSetOrder(1))	

For i:= 1 To Len(aSIX)
	If !Empty(aSIX[i,1])
		If !DbSeek(aSIX[i,1]+aSIX[i,2])
			RecLock("SIX",.T.)
			lDelInd := .F.
		Else
			RecLock("SIX",.F.)
			lDelInd := .T. //Se for alteracao precisa apagar o indice do banco
		EndIf
		
		If UPPER(AllTrim(CHAVE)) != UPPER(Alltrim(aSIX[i,3]))
			if aScan(aArqUpd, aSIX[i,1]) == 0
				aAdd(aArqUpd,aSIX[i,1])
			endif			
			lSix := .T.
			If !(aSIX[i,1]$cAlias)
				cAlias += aSIX[i,1]+"/"
			EndIf
			For j:=1 To Len(aSIX[i])
				If FieldPos(aEstrut[j])>0
					FieldPut(FieldPos(aEstrut[j]),aSIX[i,j])
				EndIf
			Next j
			MsUnLock()
			If lDelInd
				TcInternal(60,RetSqlName(aSix[i,1]) + "|" + RetSqlName(aSix[i,1]) + aSix[i,2]) //Exclui sem precisar baixar o TOP
			Endif	
		EndIf
		IncProc("Atualizando ndices...")
	EndIf
Next i

Return

/*

Ŀ
Funo    MyOpenSM0Ex Autor Sergio Silveira        Data 07/01/2003
Ĵ
Descrio  Efetua a abertura do SM0 exclusivo                         
Ĵ
 Uso       Atualizacao FIS                                            
ٱ

*/
Static Function MyOpenSM0Ex()

Local lOpen := .F. 
Local nLoop := 0 

For nLoop := 1 To 20
	dbUseArea( .T.,, "SIGAMAT.EMP", "SM0", .F., .F. ) 	
	If !Empty( Select( "SM0" ) ) 
		lOpen := .T. 
		dbSetIndex("SIGAMAT.IND") 
		Exit	
	EndIf
	Sleep( 500 ) 
Next nLoop 

If !lOpen
	Aviso( "Atencao !", "Nao foi possivel a abertura da tabela de empresas de forma exclusiva !", { "Ok" }, 2 ) 
EndIf                                 

Return( lOpen )